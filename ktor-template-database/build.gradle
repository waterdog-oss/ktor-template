apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencies {
    api project(":ktor-template-core")

    api deps.exposed.core
    api deps.exposed.dao
    api deps.exposed.jdbc
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId "ktor-template-database"
            version project.version

            pom {
                name = 'Waterdog Ktor service template database handling utilities'
                description = 'Provides support for database connectivity for projects using the ktor-template-core'
                url = 'https://github.com/waterdog-oss/ktor-template'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/waterdog-oss/ktor-template'
                    }
                }
                developers {
                    developer {
                        id = "bruno.felix"
                        name = "Bruno Felix"
                        email = "bruno.felix@waterdog.mobi"
                    }
                    developer {
                        id = "goncalo.ferreira"
                        name = "Gon√ßalo Ferreira"
                        email = "goncalo@waterdog.mobi"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/aterdog-oss/ktor-template.git'
                    developerConnection = 'scm:git:git://github.com:waterdog-oss/ktor-template.git'
                    url = 'https://github.com/waterdog-oss/ktor-template'
                }
            }

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}
